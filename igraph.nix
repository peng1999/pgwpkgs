{ stdenv, lib, fetchFromGitHub,
  pkg-config, autoreconfHook,
  flex, yacc, zlib, libxml2 }:

stdenv.mkDerivation rec {
  name = "igraph";
  version = "0.5.8";

  src = fetchFromGitHub {
    owner = "igraph";
    repo = name;
    rev = version;
    sha256 = "sha256-3h+dpdvyL0FHEqPrdtMqqvRnealS8QUlYCJxPFUbOmc=";
  };

  prePatch = ''
    sed -i '50i #ifdef bool\n#undef bool\n#endif' src/types.h
  '';

  nativeBuildInputs = [ pkg-config autoreconfHook ];
  buildInputs = [ flex yacc zlib libxml2 ];

  # This file is normally generated by igraph's bootstrap.sh, but we can do it
  # ourselves. ~ C.
  postPatch = ''
    echo "${version}" > VERSION
  '';

  meta = {
    description = "The network analysis package";
    homepage = "http://igraph.org/";
    license = lib.licenses.gpl2;
    maintainers = [ lib.maintainers.MostAwesomeDude ];
  };
}
